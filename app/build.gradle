apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.genymobile.gnirehtet"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
/*
            Put this in ~/.gradle/gradle.properties:
            RELEASE_STORE_FILE={path to your keystore}
            RELEASE_STORE_PASSWORD=*****
            RELEASE_KEY_ALIAS=*****
            RELEASE_KEY_PASSWORD=*****
*/
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

task forwardPort(type: Exec) {
    // linux
    commandLine 'adb', 'reverse', 'tcp:1080', 'tcp:1080'
}

task start(type: Exec, dependsOn: ['installDebug', 'forwardPort']) {
    // linux
    commandLine 'adb', 'shell', 'am', 'startservice', '-a', 'com.genymobile.gnirehtet.START', '--esa', 'dnsServers', '8.8.8.8'
}

task stop(type: Exec) {
    // linux
    commandLine 'adb', 'shell', 'am', 'startservice', '-a', 'com.genymobile.gnirehtet.STOP'
}
